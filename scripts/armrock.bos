//linear constant 163840

piece hips,gun,chest,rthigh,lthigh,lshin,rshin,rfoot,lfoot,muzzle, exhaust, turner, aimpoint;

static-var isMoving, isAiming, shooting_num;

// Signal definitions
#define SIG_AIM				2
#define SIG_AIM_2			4
#define SIG_MOVE			16

#include "constants.h"		

DamageControl()
{
	while (get BUILD_PERCENT_LEFT) {
		sleep 1000;
	}
	var health;
	while (TRUE)
	{
		health = get HEALTH;
		if (health < 50)
		{
			emit-sfx SFXTYPE_WHITESMOKE from hips;
			if (health < 25)
			{	
				emit-sfx SFXTYPE_BLACKSMOKE from chest;
			}
		}
		sleep 1000;
	}
}


walk()
{
	turn rthigh 	to y-axis <0> 		speed <135>;
	turn lthigh 	to y-axis <0> 		speed <130>;
	
	turn rthigh 	to z-axis <0> 		speed <135>;
	turn lthigh 	to z-axis <0> 		speed <130>;
	turn lfoot 	to z-axis <0> 		speed <130>;
	turn rfoot 	to z-axis <0> 		speed <130>;
			
	if (isMoving)
	{
			Turn rshin to x-axis <85> speed<260>;	
			Turn rthigh to x-axis <-100> speed<135>;
			Turn lthigh to x-axis <30> speed<135>;
			Turn chest to y-axis <10> speed<60>;
			wait-for-move hips along y-axis;		
			move hips to y-axis [1.2] speed <4200>;
			wait-for-move hips along y-axis;
			turn rshin to x-axis <10> speed <315>;
			move hips to y-axis [0] speed <4200>;
	}
	if (isMoving)
	{
			Turn lshin to x-axis <85> speed<260>;
			Turn lthigh to x-axis <-100> speed<135>;
			turn rthigh to x-axis <30> speed <135>;
			Turn chest to y-axis <-10> speed<60>;
			wait-for-move hips along y-axis;
			move hips to y-axis [1.2] speed <4200>;	
			wait-for-move hips along y-axis;
			turn lshin to x-axis <10> speed <315>;
			move hips to y-axis [0] speed <4200>;
	}
}


shootwalk()
{
	turn rthigh 	to y-axis <0> 		speed <135>;
	turn lthigh 	to y-axis <0> 		speed <130>;
	
	turn rthigh 	to z-axis <0> 		speed <135>;
	turn lthigh 	to z-axis <0> 		speed <130>;
	turn lfoot 	to z-axis <0> 		speed <130>;
	turn rfoot 	to z-axis <0> 		speed <130>;
		
	if (isMoving)
	{
			Turn rshin to x-axis <85> speed<260>;	
			Turn rthigh to x-axis <-100> speed<135>;
			Turn lthigh to x-axis <30> speed<135>;
			wait-for-move hips along y-axis;		
			move hips to y-axis [1.2] speed <4200>;
			wait-for-move hips along y-axis;
			turn rshin to x-axis <10> speed <315>;
			move hips to y-axis [0] speed <4200>;
	}		
	if (isMoving)
	{
			Turn lshin to x-axis <85> speed<260>;
			Turn lthigh to x-axis <-100> speed<135>;
			turn rthigh to x-axis <30> speed <135>;
			wait-for-move hips along y-axis;
			move hips to y-axis [1.2] speed <4200>;	
			wait-for-move hips along y-axis;
			turn lshin to x-axis <10> speed <315>;
			move hips to y-axis [0] speed <4200>;
	}
}


stopwalk ()
{
	turn lfoot		to x-axis <0> 		speed <395>;
	turn rfoot		to x-axis <0> 		speed <395>;
	turn rthigh 	to x-axis <0> 		speed <235>;
	turn lthigh 	to x-axis <0> 		speed <230>;
	turn lshin  	to x-axis <0> 		speed <235>;
	turn rshin 	to x-axis <0> 		speed <230>;
	
	turn rthigh 	to y-axis <-20> 	speed <135>;
	turn lthigh 	to y-axis <20> 		speed <130>;
	
	turn rthigh 	to z-axis <3> 		speed <135>;
	turn lthigh 	to z-axis <-3> 		speed <130>;
	turn lfoot 	to z-axis <3> 		speed <130>;
	turn rfoot 	to z-axis <-3>		speed <130>;
}



StartMoving()
{
	isMoving = TRUE;
}

StopMoving()
{
	isMoving = FALSE;
}

MotionControl()
{
	while ( TRUE )
	{              
		IF (isMoving)
		{
			IF (isAiming)
			{
				CALL-SCRIPT shootwalk();
			}
                else
			{
				CALL-SCRIPT shootwalk();
			}
		}
		else
		{
			CALL-SCRIPT stopwalk();
			sleep 120; 
		}
	}
}

Create()
{
	isMoving = FALSE;
	isAiming = FALSE;
	shooting_num=1;
	start-script MotionControl();	
	start-script DamageControl();
	Turn chest to y-axis <-20> now;
	//Turn gun to x-axis <5> now;
}
	
AimFromWeapon1(piecenum)
{
	piecenum=aimpoint;
}

QueryWeapon1(piecenum)
{
	piecenum = muzzle;
}

//---------------------------------------------------------------------
//gun functions;
//---------------------------------------------------------------------	
RestoreAfterDelay()
{
	sleep 2000;
	turn turner to y-axis <0> speed <100>;
	Turn gun to x-axis <20> speed <50>;
	isAiming = FALSE;
}

AimWeapon1(heading,pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	isAiming = TRUE;
	turn hips to x-axis <0> now;
	turn chest to x-axis <0> now;
	turn gun to x-axis <0> - pitch speed <90>;
	turn turner to y-axis heading + <4> speed <180>;
	wait-for-turn turner around y-axis;
	wait-for-turn gun around x-axis;
	start-script RestoreAfterDelay();
	
	return(TRUE);
}
	
FireWeapon1()
{
	emit-sfx 1024 from muzzle;
}
	

Killed( severity, corpsetype )
{
	if (severity <= 25)
	{
		corpsetype = 1;
		explode hips type		BITMAPONLY;
		explode chest type		BITMAPONLY;
		return( 0 );
	}

	if (severity <= 50)
	{
		corpsetype = 1;
		explode hips type		SHATTER;
		explode chest type		SHATTER;
		explode gun type		SHATTER;
		return( 0 );
	}

	corpsetype = 2;
	explode hips type		SHATTER;
	explode chest type		SHATTER;
	explode gun type		SHATTER;
	
	return( 0 );
}