//linear constant 163840

#include "constants.h"

piece		base, body, turret, arms, firepoint1, firepoint2, exhaust1, exhaust2, gun, cab, connection,
			rwheel1, rwheel2, rwheel3,
			lwheel1, lwheel2, lwheel3,
			gs1r, gs2r, gs3r,
			gs1l, gs2l, gs3l;

static-var	moving, once, animCount, curGun, firepoint, deployed, 
			s1r, s2r, s3r,
			s1l, s2l, s3l,
			xtilt, xtiltv, xtilta, ztilt, ztilta, ztiltv, 
			ya, yv, yp, runsp, WHEEL_TURN_SPEED;

// Signal definitions
#define SIG_AIM1						1
#define ANIM_SPEED						50
#define RESTORE_DELAY					3000

#define TURRET_TURN_SPEED				<240>
#define GUN_TURN_SPEED					<60>
#define ARMS_MOVE_SPEED					[10]

#define ANIM_PERIOD				50
#define PIVOT_MOD				11 //appox. equal to MAX_PIVOT / turnrate
#define MAX_PIVOT				<16>
#define MIN_PIVOT				<-16>
#define PIVOT_SPEED				<60>

RestoreAfterDelay(delay)
{
	sleep delay;
	turn turret to y-axis 0 speed TURRET_TURN_SPEED/2;
	turn gun to x-axis 0 speed GUN_TURN_SPEED/2;
}

AnimControl() {
	var lastHeading, currHeading, diffHeading, pivotAngle;
	lastHeading = GET HEADING;
	while (1) {
	
		//pivot
		currHeading = GET HEADING;
		diffHeading = currHeading - lastHeading;
		if (diffHeading > 0x7fff) diffHeading = diffHeading - 0x10000;
		if (diffHeading < -0x8000) diffHeading = diffHeading + 0x10000;
		pivotAngle = diffHeading * PIVOT_MOD;
		if (pivotAngle > MAX_PIVOT) pivotAngle = MAX_PIVOT;
		if (pivotAngle < MIN_PIVOT) pivotAngle = MIN_PIVOT;
		turn cab to y-axis pivotAngle speed PIVOT_SPEED;
		turn base to y-axis 0 - pivotAngle speed PIVOT_SPEED;
		
		lastHeading = currHeading;
		sleep ANIM_PERIOD;
	}
}

Suspension()
{
   while(1)
   {
   
   while(runsp)
   {   
      s1r = get GROUND_WATER_HEIGHT(get PIECE_XZ(gs1r)) - get PIECE_Y(gs1r);
      if(s1r < [-1])
      {
         s1r = [-1];
      }
      if(s1r > [1])
      {
         s1r = [1];
      }

      s2r = get GROUND_WATER_HEIGHT(get PIECE_XZ(gs2r)) - get PIECE_Y(gs2r);
      if(s2r < [-1])
      {
         s2r = [-1];
      }
      if(s2r > [1])
      {
         s2r = [1];
      }
      
      s3r = get GROUND_WATER_HEIGHT(get PIECE_XZ(gs3r)) - get PIECE_Y(gs3r);
      if(s3r < [-1])
      {
         s3r = [-1];
      }
      if(s3r > [1])
      {
         s3r = [1];
      }
      
      s1l = get GROUND_WATER_HEIGHT(get PIECE_XZ(gs1l)) - get PIECE_Y(gs1l);
      if(s1l < [-1])
      {
         s1l = [-1];
      }
      if(s1l > [1])
      {
         s1l = [1];
      }
      
      s2l = get GROUND_WATER_HEIGHT(get PIECE_XZ(gs2l)) - get PIECE_Y(gs2l);
      if(s2l < [-1])
      {
         s2l = [-1];
      }
      if(s2l > [1])
      {
         s2l = [1];
      }

      s3l = get GROUND_WATER_HEIGHT(get PIECE_XZ(gs3l)) - get PIECE_Y(gs3l);
      if(s3l < [-1])
      {
         s3l = [-1];
      }
      if(s3l > [1])
      {
         s3l = [1];
      }
      
      
      xtilta = 0 - (s1r - s3r + s1l - s3l)/58000 + xtiltv/7;      
      xtiltv = xtiltv + xtilta;
      xtilt = xtilt + xtiltv*4;
      
      ztilta = 0 + (s1r + s3r - s1l - s3l)/58000 - ztiltv/7;
      ztiltv = ztiltv + ztilta;
      ztilt = ztilt + ztiltv*4;
      
      ya = (s1r + s3r + s1l + s3l)/100 - yv/25;
      yv = yv + ya;
      yp = yp + yv/10;
      
      move base to y-axis yp speed <9000>;
      turn base to x-axis xtilt speed <9000>;
      turn base to z-axis ztilt speed <9000>;
      
      move rwheel1 to y-axis s1r speed <9000>;
      move rwheel2 to y-axis s2r speed <9000>;
      move rwheel3 to y-axis s3r speed <9000>;
      
      move lwheel1 to y-axis s1l speed <9000>;
      move lwheel2 to y-axis s2l speed <9000>;
      move lwheel3 to y-axis s3l speed <9000>;
      
      WHEEL_TURN_SPEED = ((get CURRENT_SPEED)/2);
      
	spin rwheel1 around x-axis speed WHEEL_TURN_SPEED;
	spin rwheel2 around x-axis speed WHEEL_TURN_SPEED;
	spin rwheel3 around x-axis speed WHEEL_TURN_SPEED;
	spin lwheel1 around x-axis speed WHEEL_TURN_SPEED;
	spin lwheel2 around x-axis speed WHEEL_TURN_SPEED;
	spin lwheel3 around x-axis speed WHEEL_TURN_SPEED;
	
      sleep 10;
      }
      sleep 10;
   } 
}

DamageControl()
{
	var health;
	while (TRUE)
	{
		health = get HEALTH;
		
		// Restore damaged parts
/*		if (health > 25)
		{
			if (health > 50)
			{
				
			}

		}*/
		
		// Damage parts, smoke emits etc.
		if (health < 50)
		{
			emit-sfx SFXTYPE_WHITESMOKE from body;
			if (health < 25)
			{	
				emit-sfx SFXTYPE_BLACKSMOKE from turret;
			}
		}
		sleep 1000;
	}
}

Deploy()
{
	if (moving == 1)
	{
		deployed = 0;
		turn turret to y-axis 0 speed TURRET_TURN_SPEED/2;
		turn gun to x-axis 0 speed GUN_TURN_SPEED/2;
		move arms to y-axis 0 speed ARMS_MOVE_SPEED;
	}
	if (moving == 0)
	{
		move arms to y-axis [4] speed ARMS_MOVE_SPEED;
		wait-for-move arms along y-axis;
		deployed = 1;
	}
}

StopMoving()
{
	moving = FALSE;
	call-script Deploy();
	
	sleep 500;
	if	(!moving)
	{
		once = animCount*ANIM_SPEED/1000;
		if (once > 3) once = 3;
	
		stop-spin rwheel1 around x-axis;
		stop-spin rwheel2 around x-axis;
		stop-spin rwheel3 around x-axis;
		stop-spin lwheel1 around x-axis;
		stop-spin lwheel2 around x-axis;
		stop-spin lwheel3 around x-axis;
	
		runsp = FALSE;
	}
}

StartMoving()
{
	runsp = TRUE;
	moving = TRUE;
	animCount = 0;
	call-script Deploy();
	
	spin rwheel1 around x-axis speed WHEEL_TURN_SPEED;
	spin rwheel2 around x-axis speed WHEEL_TURN_SPEED;
	spin rwheel3 around x-axis speed WHEEL_TURN_SPEED;
	spin lwheel1 around x-axis speed WHEEL_TURN_SPEED;
	spin lwheel2 around x-axis speed WHEEL_TURN_SPEED;
	spin lwheel3 around x-axis speed WHEEL_TURN_SPEED;
	
}

// Weapons
AimFromWeapon1(piecenum)
{
	piecenum = gun;
}

QueryWeapon1(piecenum)
{
	piecenum = firepoint;
}

AimWeapon1 (heading, pitch)
{
	signal SIG_AIM1;
	set-signal-mask SIG_AIM1;
	
	if (deployed == 0 || moving == 1)
	{
		return (0);
	}
	else
	{
		turn turret to y-axis (heading) speed TURRET_TURN_SPEED;
		turn gun to x-axis (0-pitch) speed GUN_TURN_SPEED;
		wait-for-turn turret around y-axis;
		wait-for-turn gun around y-axis;
		start-script RestoreAfterDelay(RESTORE_DELAY);

		return (TRUE);
	}
}

Shot1(placeholder)
{
	if (!curGun) {
		emit-sfx UNIT_SFX1 from firepoint1;
		emit-sfx UNIT_SFX2 from exhaust1;
		firepoint = firepoint1;
	}
	else {
		emit-sfx UNIT_SFX1 from firepoint2;
		emit-sfx UNIT_SFX2 from exhaust2;
		firepoint = firepoint2;
	}
	curGun = !curGun;
}

Killed(severity, corpsetype)
{
	if(severity >= 0 AND severity < 25)
	{
		corpsetype = 1;
		explode gun type BITMAPONLY | BITMAP;
		explode turret type BITMAPONLY | BITMAP;
		explode body type BITMAPONLY | BITMAP;
	}
	else if(severity >= 25 AND severity < 50)
	{
		corpsetype = 1;
		explode gun type FALL | BITMAP;
		explode turret type SHATTER | BITMAP;
		explode body type BITMAPONLY | BITMAP;
	}
	else if(severity >= 50 AND severity < 100)
	{
		corpsetype = 2;
		explode gun type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP;
		explode turret type SHATTER | BITMAP;
		explode body type BITMAPONLY | BITMAP;
	}
	// D-Gunned/Self-D
	else if(severity >= 100)
	{
		corpsetype = 2;
		explode gun type SHATTER | BITMAP;
		explode turret type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP;
		explode body type SHATTER | BITMAP;
	}
}

Create()
{
	moving = FALSE;
	curGun = 0;
	call-script Deploy();
	
	while (get BUILD_PERCENT_LEFT)
	{
		sleep 250;
	}
	start-script suspension();
	start-script AnimControl();
	start-script DamageControl();
}